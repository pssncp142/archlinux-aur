# Maintainer: Bert Peters <bert@bertptrs.nl>
# Contributor: Alexander Sch√§ferdiek <alexander@schaeferdiek.eu>
# Contributor: Florian Klink <flokli@flokli.de>

pkgbase=spotifyd
pkgname=('spotifyd' 'spotifyd-pulseaudio')
pkgver=0.2.2
pkgrel=1
arch=('x86_64' 'armv7h' 'aarch64')
license=('GPL3')
depends=('alsa-lib' 'libogg' 'gcc-libs')
makedepends=('cargo' 'libpulse')
pkgdesc="A spotify playing daemon"
url="https://github.com/Spotifyd/$pkgbase"
source=("$pkgbase-$pkgver.tar.gz::https://github.com/Spotifyd/$pkgbase/archive/v$pkgver.tar.gz"
		"https://www.openssl.org/source/openssl-1.1.0g.tar.gz"
		"openssl.patch")
sha256sums=('6b79072efbfe1f3c831e898da2c340d806f778d598e6797347e5605d2201ec43'
			'de4d501267da39310905cb6dc8c6121f7a2cad45a7707f76df828fe1b85073af'
			'SKIP')

build() {
 
  
  cd "$srcdir/openssl-1.1.0g"	
	
  ./config -fPIC --prefix="$srcdir/openssl-build" --openssldir="$srcdir/openssl-build/ssl"
  make -j4
  make install

  cd "$srcdir/spotifyd-$pkgver"
 
  export OPENSSL_DIR="$srcdir/openssl-build"
  export OPENSSL_STATIC=yes

  patch -p1 < ../openssl.patch

  cargo build --release
  cargo build --release --features "pulseaudio_backend dbus_mpris"
}

package_spotifyd() {
  cargo install --root "$pkgdir/usr" --path "$srcdir/$pkgbase-$pkgver"
  rm "$pkgdir/usr/.crates.toml"
  install -D -m 644 "$srcdir/$pkgbase-$pkgver/contrib/spotifyd.service" "$pkgdir/usr/lib/systemd/user/spotifyd.service"
}

package_spotifyd-pulseaudio() {
  depends=(libpulse)
  conflicts=(spotifyd)
  pkgdesc="$pkgdesc, with pulseaudio support"
  cargo install --root "$pkgdir/usr" --path "$srcdir/$pkgbase-$pkgver" --features "pulseaudio_backend dbus_mpris"
  rm "$pkgdir/usr/.crates.toml"
  install -D -m 644 "$srcdir/$pkgbase-$pkgver/contrib/spotifyd.service" "$pkgdir/usr/lib/systemd/user/spotifyd.service"
}

